subroutine teos_q2hprho(q,h,pre,rho,ened)
!

use def_teos_parameter
use phys_constant, only: long
implicit none
!
  real(8), intent(inout) :: q
  real(8), intent(out)   :: h, pre, rho, ened
  integer                :: iphase, i0
  real(long), external   :: lagint_4th
  real(long)             :: x4(4), f4(4), qloc
  real(8)                :: hin, qin, abin, abct, fac1, fac2, fack, small
!
  if (q <= qi(0)) then
    iphase = 1

    hin  = hi(0)
    qin  = qi(0)
    abin = gamma_crust
    abct = kappa_crust
    fac1 = 1.0d0/(abin - 1.0d0)
    fac2 = abin/(abin - 1.0d0)
    fack = abct**(-fac1)
    small = 1.0d-60
    if (q <= small) q = small
    h = hin + fac2*(q - qin)
    if (h <= 1.0d0) h = 1.0d0
    pre = fack*q**fac2
    rho = fack*q**fac1
    ened = rho*h - pre
    return
  end if

  call teos_lookup(q, qi, iphase)

  i0 = min0(max0(iphase-2,0),nphase-3)
  qloc = q

  x4(1:4) = qi(i0:i0+3)
  f4(1:4) = hi(i0:i0+3)
  h       = lagint_4th(x4,f4,qloc)
  if (h <= 1.0d0) h = 1.0d0

  f4(1:4) = prei(i0:i0+3)
  pre     = lagint_4th(x4,f4,qloc)

  f4(1:4) = rhoi(i0:i0+3)
  rho     = lagint_4th(x4,f4,qloc)

  f4(1:4) = enei(i0:i0+3)
  ened    = lagint_4th(x4,f4,qloc)
!
end subroutine teos_q2hprho
